(%testspec
 (%tags))

(module
  (import graphics)

  (extern nanotime () -> u64)
  (define (main)
    (let ((start (nanotime)))
      (let ((img (kernel ((i (iota 1024)))
                   (kernel ((j (iota 1024)))
                     (let ((cr (- (/ (int->float j) 512.0) 1.5))
                           (ci (- (/ (int->float i) 512.0) 1.0))
                           (xr 0.0)
                           (xi 0.0)
                           (idx 0)
                           (escape 0))
                       (while (< idx 256)
                         (let ((xrp (+ (- (* xr xr) (* xi xi)) cr))
                               (xip (+ (* 2.0 (* xr xi)) ci)))
                           (set! xr xrp)
                           (set! xi xip)
                           (let ((m (+ (* xr xr) (* xi xi))))
                             (if (< m 4.0)
                                 (set! escape idx))
                             (set! idx (+ idx 1)))))
                       escape)))))
        (let ((stop (nanotime)))
          (println* (length img) "x" (length (vector-ref img 0)))
          (write-pgm "test.bin/mandelbrot.pgm" img)
          (print "Time to generate Mandelbrot Set in milliseconds:\n")
          (print (/ (- stop start) 1000000))
          (print "\n")
          0)))))
